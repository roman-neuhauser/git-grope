# Maintainer: Roman Neuhauser <neuhauser+git-grope@sigpipe.cz>
pkgbase=git-grope
pkgname=(git-grope grope)
pkgver=__VERSION__
pkgrel=1
arch=(any)
url="https://github.com/roman-neuhauser/$pkgname"
license=(MIT)
depends=(zsh)
checkdepends=(cram zsh)
source=($pkgname-$pkgver.tar.gz)
sha1sums=(SKIP)

build() {
  _make most
}

check() {
  _make check
}

package_git-grope() {
  pkgdesc="List tracked files matching patterns"
  depends+=(git)
  checkdepends+=(git)
  _make install-git-grope DESTDIR="$pkgdir/"
}

package_grope() {
  pkgdesc="List files matching patterns"
  _make install-grope DESTDIR="$pkgdir/"
}

_make() {
  cd "$srcdir/$pkgbase-$pkgver"
  make PREFIX=/usr MANDIR=/usr/share/man "$@"
}

